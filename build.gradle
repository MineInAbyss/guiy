buildscript {
    ext.kotlin_version = '1.3.61'

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
}

//Stops errors when a parent project defines the version being used for a plugin like kotlin
ext.addPlugin = { String pluginId -> if (!project.getPlugins().hasPlugin(pluginId)) apply plugin: pluginId }

addPlugin('org.jetbrains.kotlin.jvm')

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(project.hasProperty("snapshot") && snapshot.toBoolean() != false) {
    version += "-SNAPSHOT"
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'dre-repo'
        url = 'https://erethon.de/repo/'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MineInAbyss/guiy")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'de.erethon:headlib:3.0.2'
    implementation 'org.spigotmc:spigot-api:1.15-R0.1-SNAPSHOT'
    implementation 'com.google.protobuf:protobuf-java:3.8.0'
    implementation 'com.google.auto.value:auto-value-annotations:1.6.3'
    implementation 'com.google.dagger:dagger:2.+'
    compileOnly 'org.jetbrains:annotations:17.0.0'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.3'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.+'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}