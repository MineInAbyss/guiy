plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'io.github.0ffz.github-packages' version '1.0.3'
}

if (project.hasProperty("buildNo")) version += ".$buildNo"

java {
    setSourceCompatibility(1.8)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://erethon.de/repo/' }
    maven githubPackage.invoke("MineInAbyss/KotlinSpice")
}

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/guiy")
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    compileOnly "org.cultofclang.minecraft:kotlinspice:$kotlin_version-8"
    compileOnly "org.spigotmc:spigot-api:$server_version"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly 'org.jetbrains:annotations:17.0.0'
    implementation 'de.erethon:headlib:3.0.5'
    implementation 'com.google.protobuf:protobuf-java:3.8.0'
//    implementation 'com.google.dagger:dagger:2.28.1'

    testImplementation 'junit:junit:4.12'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.28.1'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [version: version]
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xjvm-default=compatibility"
    }
}
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }