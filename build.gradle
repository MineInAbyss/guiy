plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

if(project.hasProperty("snapshot") && snapshot.toBoolean() != false) {
    version += "-SNAPSHOT"
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'dre-repo'
        url = 'https://erethon.de/repo/'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MineInAbyss/guiy")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'de.erethon', name: 'headlib', version: '3.0.1'
    implementation 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.8.0'
    implementation "com.google.auto.value:auto-value-annotations:1.6.3"
    implementation 'com.google.dagger:dagger:2.+'
    compileOnly 'org.jetbrains:annotations:17.0.0'
    annotationProcessor "com.google.auto.value:auto-value:1.6.3"
    annotationProcessor 'com.google.dagger:dagger-compiler:2.+'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
