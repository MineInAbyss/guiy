plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
    id 'org.jetbrains.kotlin.jvm'
}

if (project.hasProperty("buildNo")) version += ".$buildNo"

java {
    setSourceCompatibility(1.8)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://erethon.de/repo/' }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MineInAbyss/guiy")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15-R0.1-SNAPSHOT'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly 'org.jetbrains:annotations:17.0.0'
    implementation 'de.erethon:headlib:3.0.2'
    implementation 'com.google.protobuf:protobuf-java:3.8.0'
    implementation 'com.google.dagger:dagger:2.28.1'

    testImplementation 'junit:junit:4.12'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.28.1'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [version: version]
}

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }